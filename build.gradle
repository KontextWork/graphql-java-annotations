import aQute.bnd.gradle.Bundle

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:6.4.0'
    }
}
plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'com.github.hierynomus.license' version '0.16.1'
    id "biz.aQute.bnd.builder" version "6.4.0"
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

repositories {
    mavenCentral()
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        doLast {
            options.compilerArgs += "-parameters"
        }
    }
}

dependencies {
    implementation 'javax.validation:validation-api:2.0.1.Final'
    // @see https://mvnrepository.com/artifact/com.graphql-java/graphql-java
//    implementation 'com.graphql-java:graphql-java:21.3'
    implementation 'com.graphql-java:graphql-java:22.1'
    // @see https://mvnrepository.com/artifact/com.graphql-java/graphql-java-extended-scalars
//    implementation 'com.graphql-java:graphql-java-extended-scalars:21.0'
    implementation 'com.graphql-java:graphql-java-extended-scalars:22.0'
    implementation 'javax.xml.bind:jaxb-api:2.3.1'

    // OSGi
    compileOnly 'org.osgi:osgi.core:8.0.0'
    compileOnly 'org.osgi:org.osgi.service.cm:1.6.0'
    compileOnly 'org.osgi:org.osgi.service.component:1.5.1'
    compileOnly 'biz.aQute.bnd:biz.aQute.bndlib:3.5.0'

    testImplementation 'org.testng:testng:7.8.0'
    // @see https://mvnrepository.com/artifact/org.hamcrest/hamcrest-all
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'org.mockito:mockito-core:5.6.0'
}

test.useTestNG()

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId 'io.github.graphql-java'
            artifactId project.name
            version project.version

            artifact sourcesJar
            // FIXME: does no longer work (most probably gradle 8 related)
//            artifact javadocJar

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'graphql-java-annotations'
                    description 'Annotations-based syntax for GraphQL schema definition'
                    url 'https://github.com/KontextWork/graphql-java-annotations'
                    inceptionYear '2022'

                    scm {
                        url 'https://github.com/KontextWork/graphql-java-annotations'
                        connection 'scm:https://github.com/KontextWork/graphql-java-annotations.git'
                        developerConnection 'scm:git://github.com/KontextWork/graphql-java-annotations.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'tm'
                            name 'Thomas M.'
                            email 'tm@donotbugme.com'
                        }
                        developer {
                            id 'em'
                            name 'Eugen Mayer'
                            email 'em@donotbugme.com'
                        }
                    }
                }
            }
        }
    }
}


publishing {
    repositories {
        maven {
            def releasesRepoUrl = "https://nexus.kw.kontextwork.com/repository/kw-maven-release"
            def snapshotsRepoUrl = "https://nexus.kw.kontextwork.com/repository/kw-maven-snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            if(project.hasProperty("ourNexusAuthorUser")) {
                credentials(PasswordCredentials) {
                    username "$ourNexusAuthorUser"
                    password "$ourNexusAuthorPassword"
                }
            }
        }
    }
}

task bundle(type: Bundle) {
    from sourceSets.main.output
    bndfile = file('bundle.bnd')
}
